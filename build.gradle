buildscript {
  repositories {
    mavenRepo name: 'clojars', url: 'http://clojars.org/repo'
  }
  dependencies {
    classpath 'org.clojars.viacoban:clojuresque:1.5.3-SNAPSHOT'
  }
}

apply plugin: 'idea'

subprojects {
  group = 'crackle'
  version = '0.4.0-SNAPSHOT'

  apply plugin: 'java'
  apply plugin: 'clojure'
  apply plugin: 'idea'

  warnOnReflection = true

  repositories {
    mavenCentral()
    clojarsRepo()
    mavenRepo url: "https://repository.cloudera.com/artifactory/cloudera-repos"
  }

  configurations {
    provided
    compile.extendsFrom provided

    all*.exclude module: 'thrift'
    all*.resolutionStrategy {
      force 'org.slf4j:slf4j-api:1.4.3'
      force 'org.slf4j:slf4j-log4j12:1.4.3'
    }
  }

  dependencies {
    compile 'org.clojure:clojure:1.4.0'
  }

  idea {
    module {
      scopes.PROVIDED.plus += configurations.provided
    }
  }

  compileClojure.dependsOn compileJava

  clean.doFirst {
    ant.delete(dir: 'lib')
  }

  task writePom << {
    pom {
      project {
        description 'A Clojure wrapper for Apache Crunch'
        inceptionYear '2012'
        url 'https://github.com/viacoban/crackle'
        licenses {
          license {
            name 'Eclipse Public License'
            url 'http://www.eclipse.org/legal/epl-v10.html'
          }
        }
      }
    }.writeTo("$buildDir/pom.xml")
  }

}
