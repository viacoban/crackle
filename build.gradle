buildscript {
  repositories {
    mavenRepo name: 'clojars', url: 'http://clojars.org/repo'
  }
  dependencies {
    classpath 'clojuresque:clojuresque:1.5.4'
  }
}

apply plugin: 'idea'

ext.hadoopVersion = 'org.apache.hadoop:hadoop-client:2.0.0-mr1-cdh4.1.3'
ext.hbaseVersion = 'org.apache.hbase:hbase:0.92.1-cdh4.1.2'
ext.crunchCore = 'org.apache.crunch:crunch:0.5.0-cdh4.1.3'
ext.crunchHBase = 'org.apache.crunch:crunch-hbase:0.5.0-cdh4.1.3'

subprojects {
  group = 'crackle'
  version = '0.5.2'

  apply plugin: 'java'
  apply plugin: 'clojure'
  apply plugin: 'idea'

  warnOnReflection = true

  repositories {
    mavenCentral()
    clojarsRepo()
    mavenRepo url: "https://repository.cloudera.com/artifactory/cloudera-repos"
  }

  configurations {
    provided
    compile.extendsFrom provided

    all*.exclude module: 'thrift'
    all*.resolutionStrategy {
      force 'org.slf4j:slf4j-api:1.4.3'
      force 'org.slf4j:slf4j-log4j12:1.4.3'
    }
  }

  dependencies {
    compile 'org.clojure:clojure:1.4.0'
    compile 'org.clojure:tools.logging:0.2.6'
  }

  idea {
    module {
      scopes.PROVIDED.plus += configurations.provided
    }
  }

  compileClojure.dependsOn compileJava

  clean.doFirst {
    ant.delete(dir: 'lib')
  }

  task writePom << {
    pom {
      project {
        description 'A Clojure wrapper for Apache Crunch'
        inceptionYear '2012'
        url 'https://github.com/viacoban/crackle'
        licenses {
          license {
            name 'Eclipse Public License'
            url 'http://www.eclipse.org/legal/epl-v10.html'
          }
        }
      }
    }.writeTo("$buildDir/pom.xml")
  }
}

project(':crackle-core') {
  apply plugin: 'maven'

  dependencies {
    compile 'com.twitter:carbonite:1.3.1'
    compile 'commons-lang:commons-lang:2.4'
    compile 'commons-io:commons-io:2.4'
    compile 'com.google.guava:guava:11.0.2'
    compile crunchCore
    provided hadoopVersion
  }
}

project(':crackle-hbase') {
  dependencies {
    compile project(':crackle-core')
    compile crunchHBase

    provided hadoopVersion
    provided hbaseVersion
  }
}

project (':crackle-example') {
  dependencies {
    compile project(':crackle-core')
    compile project(':crackle-hbase')

    runtime hadoopVersion
    runtime hbaseVersion

    runtime 'log4j:log4j:1.2.17'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.4'
}
